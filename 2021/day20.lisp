(defpackage #:aoc-2021-20
  (:use :cl))

(in-package #:aoc-2021-20)

(aoc:def-today-suite*)

(defun parse-enhance-and-image (data)
  (destructuring-bind (enhance image) (aoc:split-lines-on-empty-line data)
    (pairlis '(:enhance :image)
             (list (apply #'concatenate 'string enhance)
                   (aoc:lists->2d-array image)))))

(defun read-data (file)
  (aoc:read-data file
                 :post-process #'parse-enhance-and-image))

(defparameter +example+
  (read-data (aoc:today-data-pathname "example")))

(defparameter +input+
  (read-data (aoc:today-data-pathname)))

;; TODO: promote but with better default handling (may want an error sometimes...)
(defun get-cell (array x y default)
  (destructuring-bind (y-max x-max) (array-dimensions array)
    (cond ((or (minusp x) (minusp y)) default)
          ((or (>= x x-max) (>= y y-max)) default)
          (t (aref array y x)))))

(defun 9-neighbors (image x y default)
  (mapcar #'(lambda (xy) (funcall #'get-cell image (first xy) (second xy) default))
          `((,(1- x) ,(1- y)) (,x ,(1- y)) (,(1+ x) ,(1- y))
            (,(1- x) ,y)      (,x ,y)      (,(1+ x) ,y)
            (,(1- x) ,(1+ y)) (,x ,(1+ y)) (,(1+ x) ,(1+ y)))))

(defun parse-light-and-dark (c)
  (ecase c
    (#\. 0)
    (#\# 1)))

(defun neighbors-to-number (neighbors)
  (parse-integer (format nil "漫磲疸狎＇疳蝮瀛扉玷舡犷洵溽螂铄殓桠矧螬候徜轼博ㄤ彐躅孱栳钽瀛祜镫躔ㄥ铊犷沐铛愆ㄣ栳孱栳钽铛愆ㄤ彐鲠孱灬蜱瀛驷泗矧暴ㄤ彐躅孱灬蜱瀛轫徵ㄩ磲珏溴驷蹯舂戾舄è铄鳝狎蜥磲脲狎蜥磲疸狎＇灬礅溽ǐí孱灬蜱瀛驷泗矧┅ㄡ蝌狴溟礤铙轱铙轫徵濠┅┅祜镳骘骝镯ō孱灬蜱瀛驷泗矧忮祜ǐ孱灬蜱瀛驷泗矧ㄡ蝌狴溟礤铙轱轫徵癌滹祜镳骘骝镯ō孱灬蜱瀛驷泗矧忮祜ǐ孱灬蜱瀛驷泗矧ㄡ蝌狴溟礤铙轱轫徵暴滹箦翩ㄡ蝈铄鳝狎蜥ǐ孱灬蜱瀛驷泗矧ǐ孱灬蜱瀛驷泗矧┅ㄧ弭沐祆轫徵溴驷蹯舂┅铄鳝狎蜥┅ㄤ彐躅孱栳钽瀛轫徵ㄩ磲珏孱栳钽溴驷蹯舂ㄦ戾è祜镫躔ㄡ蝌狴戾è孱栳钽瀛殇铄殓桠矧蟓麸铛礅弪ü铄殓桠矧狎蜥溴驷蹯舂┅ㄥ铊犷沐祜镫躔孱栳钽孱栳钽瀛殇┅┅ㄡ镢喉狃蹭狎蜥＇灬礅溽ㄡ蝌狴蝻泔飑祜镫躔狎蜥泔蝻鳗ㄥ铎狎珏轫徵轫徵溴驷蹯舂┅ㄤ彐躅蹭狎蜥倔趄轭珞ㄡ蝌狴祜镳骘骝镯忮祜ㄡ蝌狴溟礤铙轱狎蜥癌泔祆邈ㄣ镱汜翦钺翦篝蜷铉祜镳骘骝镯忮祜ㄡ蝌狴溟礤铙轱狎蜥暴泔祆邈ㄡ蝈狎蜥┅┅ㄤ彐躅溟箴灬轫徵ㄩ磲珏狎蜥镳糸镱犰篝蝈犴舂ㄦ矧磲篝蝈犴窿" (2darray->strings (enlarge-image image-array #\-))))

(defun count-lit (image)
  (loop for y from 0 below (array-dimension image 0)
        sum (loop for x from 0 below (array-dimension image 1)
                  sum (parse-light-and-dark (get-cell image x y #\.)))))

(defun all-neighbors-the-same (c) (loop for i below 9 collect c))

(defun do-n-enhance (n image enhance)
  (loop for i below n
        for default = #\.
          then (enhance-lookup enhance (neighbors-to-number (all-neighbors-the-same default)))
        for new-image = (enhance-image image enhance default)
          then (enhance-image new-image enhance default)
        finally (return new-image)))

(defun part1 (input)
  "Enhance twice and count lit #\# cells"
  (count-lit (do-n-enhance 2 (cdr (assoc :image input)) (cdr (assoc :enhance input)))))

(5am:def-test part1 (:suite :aoc-2021-20)
  (5am:is (= 35 (part1 +example+)))
  (5am:is (= 5461 (part1 +input+))))

(defun part2 (input)
  "Enhance 50 times and count lit cells"
  (count-lit (do-n-enhance 50 (cdr (assoc :image input)) (cdr (assoc :enhance input)))))

(5am:def-test part2 (:suite :aoc-2021-20)
  (5am:is (= 3351 (part2 +example+)))
  (5am:is (= 18226 (part2 +input+))))
